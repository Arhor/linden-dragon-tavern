import com.github.gradle.node.npm.task.NpmTask

plugins {
    id "idea"
    id "java-library"
    id "org.jsonschema2dataclass"
    id "com.github.node-gradle.node"
}

node {
    version = property("app.version.node")
    download = true

    workDir = file("${rootDir}/.gradle/nodejs")
    npmWorkDir = file("${rootDir}/.gradle/npm")
    yarnWorkDir = file("${rootDir}/.gradle/yarn")
}

idea {
    module {
        excludeDirs += files(
            "$projectDir/node_modules",
        )
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.fasterxml.jackson.core:jackson-databind:${property("app.version.jackson")}"
    implementation "javax.validation:validation-api:2.0.1.Final"
    implementation "com.google.code.findbugs:jsr305:${property("app.version.findbugsJsr305")}"
}

jsonSchema2Pojo {
    targetVersion = "17"
    targetPackage = "com.github.arhor.linden.dragon.tavern.model"
    removeOldOutput = true
    includeGetters = false
    includeSetters = false
    includeDynamicAccessors = false
    includeJsr303Annotations = true
    includeJsr305Annotations = true
}

task generateJsonSchema2TypeScript(type: NpmTask, dependsOn: npmInstall) {
    group = "build"
    description = "Builds d.ts files according to the provided schemas"
    workingDir.fileValue(projectDir)
    args = ["run", "build"]
}

build {
    dependsOn generateJsonSchema2TypeScript
}
