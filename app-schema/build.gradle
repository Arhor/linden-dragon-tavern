import com.github.gradle.node.npm.task.NpmTask

plugins {
    id "idea"
    id "java-library"
    id "org.jsonschema2dataclass"
    id "com.github.node-gradle.node"
}

node {
    version = property("app.version.node")
    download = true

    workDir = file("${rootDir}/.gradle/nodejs")
    npmWorkDir = file("${rootDir}/.gradle/npm")
    yarnWorkDir = file("${rootDir}/.gradle/yarn")
}

idea {
    module {
        excludeDirs += files(
            "$projectDir/node_modules",
        )
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "com.fasterxml.jackson.core:jackson-databind:${property("app.version.jackson")}"
}

jsonSchema2Pojo {
    targetPackage = "com.github.arhor.linden.dragon.tavern.model"
}

task generateJsonSchema2TypeScript(type: NpmTask, dependsOn: npmInstall) {
    group = "build"
    description = "Builds d.ts files according to the provided schemas"
    workingDir.fileValue(projectDir)
    args = ["run", "build"]
}

build {
    dependsOn generateJsonSchema2TypeScript
}
