import com.github.gradle.node.npm.task.NpmTask
import com.github.gradle.node.npm.task.NpxTask

plugins {
    id "com.github.node-gradle.node"
    id "idea"
}

description = "app-client"

node {
    version = property("app.version.node")
    download = true

    workDir = file("${rootDir}/.gradle/nodejs")
    npmWorkDir = file("${rootDir}/.gradle/npm")
    yarnWorkDir = file("${rootDir}/.gradle/yarn")
}

idea {
    module {
        excludeDirs += files(
            "$projectDir/node_modules",
            "$projectDir/raw_data",
            "$projectDir/dist",
            "$projectDir/dist_electron",
        )
    }
}

task copyGeneratedModel(type: Copy, dependsOn: ":app-model:build") {
    from "${project(":app-model").buildDir}/generated/sources/js2ts"
    into "${projectDir}/src/generated"
}

task updateBrowserlist(type: NpxTask, dependsOn: npmInstall) {
    description = "Updates Browserslist"
    workingDir.fileValue(projectDir)
    command.set("browserslist@latest")
    args = ["--update-db"]
}

task test(type: NpmTask, dependsOn: [updateBrowserlist, copyGeneratedModel]) {
    group = "verification"
    description = "Runs JS tests in client project"
    workingDir.fileValue(projectDir)
    args = ["run", "test:unit"]
}

task build(type: NpmTask, dependsOn: test) {
    group = "build"
    description = "Builds production version of the app client"
    workingDir.fileValue(projectDir)
    args = ["run", "build"]
}
